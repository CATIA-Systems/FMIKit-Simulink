%% 
%%  TLC library with utility functions for the FMU 
%%  model description (XML) generation.
%%

%function GetFMIType(typeName) void
  %switch typeName
    %case "real_T"
    %case "real32_T"
      %return "Real"
    %case "int8_T"
    %case "uint8_T"
    %case "int16_T"
    %case "uint16_T"
    %case "int32_T"
    %case "uint32_T"
      %return "Integer"
    %case "boolean_T"
      %return "Boolean"
    %default 
      %return "" 
  %endswitch
%endfunction

%function GetFMI3Type(typeName) void
  %switch typeName
    %case "real_T"
      %return "Float64"
    %case "real32_T"
      %return "Float32"
    %case "int8_T"
      %return "Int8"
    %case "uint8_T"
      %return "UInt8"
    %case "int16_T"
      %return "Int16"
    %case "uint16_T"
      %return "UInt16"
    %case "int32_T"
      %return "Int32"
    %case "uint32_T"
      %return "UInt32"
    %case "boolean_T"
      %return "Boolean"
    %default 
      %return "" 
  %endswitch
%endfunction

%function MinMaxAttributes(port, fmiType)
  %assign attributes = ""
  %if !ISFIELD(port, "BlockName")
    %return attributes
  %endif
  %switch fmiType
    %case "Real"
    %case "Float32"
    %case "Float64"
      %assign outmin       = FEVAL("get_param", "%<port.BlockName>", "Outmin")
      %assign outmin       = FEVAL("str2num", "%<outmin>")
      %if !ISEMPTY(outmin)
        %assign attributes = "%<attributes> min=\"%<outmin>\""
      %endif
      %assign outmax       = FEVAL("get_param", "%<port.BlockName>", "Outmax")
      %assign outmax       = FEVAL("str2num", "%<outmax>")
      %if !ISEMPTY(outmax)
        %assign attributes = "%<attributes> max=\"%<outmax>\""
      %endif
      %if ISFIELD(port, "UnitIdx")
        %assign unit = Units.Unit[port.UnitIdx].ASCIIEscapedUnitExpr
        %if !ISEMPTY(unit)
          %assign attributes = "%<attributes> unit=\"%<unit>\""
        %endif
      %endif
  %endswitch
  %return attributes
%endfunction

%function DescriptionAttribute(block)
  %assign attribute = ""
  %if !ISFIELD(block, "BlockName")
    %return attribute
  %endif
  %assign description = FEVAL("get_param", "%<block.BlockName>", "Description")
  %if !ISEMPTY(description)
    %assign attribute = " description=\"%<description>\""
  %endif
  %return attribute
%endfunction

%function GenerateSubsystemFMI(subSysIdx, vr) void
  %selectfile xmlfile
  %assign numVars = 0
  %assign subsystem = ::CompiledModel.BlockHierarchyMap.Subsystem[subSysIdx]
  %foreach bIdx = subsystem.NumBlocks
    %assign vr = GenerateBlockFMI(subsystem.Block[bIdx], subSysIdx, bIdx, vr)
  %endforeach
  %foreach ssIdx = subsystem.NumChildSubsystems
    %assign vr = GenerateSubsystemFMI(subsystem.ChildSubsystems[ssIdx], vr)
  %endforeach
  %return vr
%endfunction

%function GenerateBlockFMI(block, subSysIdx, blockIdx, vr) void
  %foreach idx = block.NumParameters
    %if block.Parameter[idx]._idx >= 0
      %assign param = ::CompiledModel.ModelParameters.Parameter[block.Parameter[idx]._idx]
      %if FEVAL("grtfmi_exclude_variable", "%<param.Identifier>", "%<VisibleParameters>")
        %continue
      %endif
      %assign vr = OutputParameterFMI(param, vr)
    %endif
  %endforeach
  %return vr
%endfunction

%function OutputParameterFMI2(param, vr)
  %assign parType      = ::CompiledModel.GlobalScope.tParametersType
  %assign defParam     = ::CompiledModel.GlobalScope.tDefaultParameters
  %assign sigSrc       = param.OwnerSysIdx
  %assign dtypeID      = LibGetRecordDataTypeId(param)
  %assign dataTypeName = SLibGetRecordDataTypeName(param, "")
  %assign valueType    = TYPE(param.Value)
  %assign width        = LibGetRecordWidth(param)
  %assign dims         = LibGetRecordDimensions(param)
  %assign nRows        = dims[0]
  %assign nCols        = SIZE(dims, 1) == 1 ? 1 : dims[1]
  %assign fmiType      = GetFMIType(dataTypeName)
  %if ISFIELD(param, "IsPseudoWorkspaceVar") && param.IsPseudoWorkspaceVar == "yes"
    %return vr
  %endif
  %if fmiType == ""
    %return vr
  %endif
  %if SIZE(dims, 1) > 2
    %return vr
  %endif
  %assign index = 0
  %foreach colid = nCols
  %foreach rowid = nRows
    %if nCols > 1 && nRows > 1
      %assign variableName = "%<param.Identifier>[%<rowid+1>,%<colid+1>]"
    %elseif width > 1
      %assign variableName = "%<param.Identifier>[%<index+1>]"
    %else
      %assign variableName = "%<param.Identifier>"  
    %endif
    %if valueType == "Matrix"
      %assign startValue = param.Value[rowid][colid]
    %elseif valueType == "Vector"
      %assign startValue = param.Value[index]
    %else
      %assign startValue = param.Value
    %endif
    %if sigSrc[1] < 0
      %assign identifier = param.Identifier
    %else
      %assign varGroupIdx = FcnSysVarGroupIndex(::CompiledModel.System[sigSrc[0]], "Parameter", sigSrc[1]) 
      %assign identifier = ::CompiledModel.VarGroups.VarGroup[varGroupIdx].Name + "." + param.Identifier
    %endif
    %selectfile xmlfile
    %switch dataTypeName
      %case "real_T"
      %case "real32_T"
        %if "%<startValue>" == "rtInf"
          %assign startValue = "INF"
        %elseif "%<startValue>" == "rtMinusInf"
          %assign startValue = "-INF"
        %elseif "%<startValue>" == "rtNaN"
          %assign startValue = "NAN"
        %else
          %assign startValue = FEVAL("strrep", "%<startValue>", "F", "")       
        %endif
        %break
      %case "uint8_T"
      %case "uint16_T"
      %case "uint32_T"
        %assign startValue = FEVAL("strrep", "%<startValue>", "U", "")
        %break
      %default
        %break
    %endswitch
    %assign startValue = FEVAL("strrep", "%<startValue>", "[", "")
    %assign startValue = FEVAL("strrep", "%<startValue>", "]", "")
    <ScalarVariable name="%<variableName>" valueReference="%<vr>" causality="parameter" variability="tunable">
      <%<fmiType> start="%<startValue>"/>
    </ScalarVariable>
    %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      return &(rtmGetDefaultParam(S)->%<identifier>);
    %assign index = index + 1
    %assign vr = vr + 1
  %endforeach
  %endforeach
  %return vr
%endfunction

%function OutputParameterFMI3(param, vr)
  %assign parType      = ::CompiledModel.GlobalScope.tParametersType
  %assign defParam     = ::CompiledModel.GlobalScope.tDefaultParameters
  %assign sigSrc       = param.OwnerSysIdx
  %assign dtypeID      = LibGetRecordDataTypeId(param)
  %assign dataTypeName = SLibGetRecordDataTypeName(param, "")
  %assign valueType    = TYPE(param.Value)
  %assign width        = LibGetRecordWidth(param)
  %assign dims         = LibGetRecordDimensions(param)
  %assign nRows        = dims[0]
  %assign nCols        = SIZE(dims, 1) == 1 ? 1 : dims[1]
  %assign fmiType      = GetFMI3Type(dataTypeName)
  %if ISFIELD(param, "IsPseudoWorkspaceVar") && param.IsPseudoWorkspaceVar == "yes"
    %return vr
  %endif
  %if fmiType == ""
    %return vr
  %endif
  %if sigSrc[1] < 0
    %assign identifier = param.Identifier
  %else
    %assign varGroupIdx = FcnSysVarGroupIndex(::CompiledModel.System[sigSrc[0]], "Parameter", sigSrc[1]) 
    %assign identifier = ::CompiledModel.VarGroups.VarGroup[varGroupIdx].Name + "." + param.Identifier
  %endif
  %selectfile xmlfile
  %assign startValue = FEVAL("grtfmi_start_value", param.Value)
  %if width == 1
    <%<fmiType> name="%<identifier>" valueReference="%<vr>" causality="parameter" variability="tunable" start="%<startValue>"/>
  %else
    <%<fmiType> name="%<identifier>" valueReference="%<vr>" causality="parameter" variability="tunable" start="%<startValue>">
      <Dimensions>
    %foreach dimidx = SIZE(dims, 1)
        <Dimension start="%<dims[dimidx]>"/>
    %endforeach
      </Dimensions>
    </%<fmiType>>
  %endif
  %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      *size = %<width>;
      return &(rtmGetDefaultParam(S)->%<identifier>);
  %assign vr = vr + 1
  %return vr
%endfunction

%function ExternalInputFMI2(port, vr)
  %assign dtypeID      = LibGetRecordDataTypeId(port)
  %assign dataTypeName = SLibGetRecordDataTypeName(port, "")
  %assign dims         = LibGetRecordDimensions(port)
  %assign width        = LibGetRecordWidth(port)
  %assign fmiType      = GetFMIType(dataTypeName)
  %assign description  = DescriptionAttribute(port)
  %assign attributes   = MinMaxAttributes(port, fmiType)
  %foreach index = width
    %if width == 1
      %assign dataName     = "%<port.Identifier>"
      %assign variableName = "%<port.Identifier>"
    %else
      %assign dataName     = "%<port.Identifier>[%<index>]"
      %assign variableName = "%<port.Identifier>[%<index+1>]"
    %endif
    %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      return &(rtmGetU(S)->%<dataName>);
    %selectfile xmlfile
    <ScalarVariable name="%<variableName>" valueReference="%<vr>" causality="input"%<description>>
      <%<fmiType> start="0"%<attributes>/>
    </ScalarVariable>
    %assign vr = vr + 1
  %endforeach
  %return vr
%endfunction

%function ExternalInputFMI3(port, vr)
  %assign dtypeID      = LibGetRecordDataTypeId(port)
  %assign dataTypeName = SLibGetRecordDataTypeName(port, "")
  %assign dims         = LibGetRecordDimensions(port)
  %assign width        = LibGetRecordWidth(port)
  %assign fmiType      = GetFMI3Type(dataTypeName)
  %assign attributes   = MinMaxAttributes(port, fmiType)
  %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      *size = %<width>;
      return &(rtmGetU(S)->%<port.Identifier>);
  %selectfile xmlfile
    %if width == 1    
    <%<fmiType> name="%<port.Identifier>" valueReference="%<vr>" causality="input" start="0"%<attributes>/>
    %else
    <%<fmiType> name="%<port.Identifier>" valueReference="%<vr>" causality="input" start="0"%<attributes>>
      <Dimensions>
        <Dimension start="%<width>"/>
      </Dimensions>
    </%<fmiType>>
    %endif
  %return vr + 1
%endfunction

%function ExternalOutputFMI2(outp, vr) Output
  %assign sysid        = outp.Block[0]
  %assign blkid        = outp.Block[1]
  %assign outportBlock = System[sysid].Block[blkid]
  %assign description  = DescriptionAttribute(outp)
  %with outportBlock
    %assign dataTypeName = SLibGetRecordDataTypeName(outp, "")
    %assign fmiType      = GetFMIType(dataTypeName)
    %assign dtypeID      = LibGetRecordDataTypeId(outp)
    %assign width        = LibGetRecordWidth(outp)
    %assign attributes   = MinMaxAttributes(outp, fmiType)
    %foreach index = width
      %if width == 1
        %assign dataName     = "%<Identifier>"
        %assign variableName = "%<Identifier>"
      %else
        %assign dataName     = "%<Identifier>[%<index>]"
        %assign variableName = "%<Identifier>[%<index+1>]"
      %endif
      %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      *size = 1;
      return &(rtmGetY(S)->%<dataName>);
      %selectfile xmlfile
    <ScalarVariable name="%<variableName>" valueReference="%<vr>" causality="output"%<description>>
      <%<fmiType>%<attributes>/>
    </ScalarVariable>
      %assign outputIndices = outputIndices + vr
      %assign vr = vr + 1
    %endforeach
  %endwith
  %return vr
%endfunction

%function ExternalOutputFMI3(outp, vr) Output
  %assign sysid        = outp.Block[0]
  %assign blkid        = outp.Block[1]
  %assign outportBlock = System[sysid].Block[blkid]
  %with outportBlock
    %assign dataTypeName = SLibGetRecordDataTypeName(outp, "")
    %assign fmiType      = GetFMI3Type(dataTypeName)
    %assign dtypeID      = LibGetRecordDataTypeId(outp)
    %assign width        = LibGetRecordWidth(outp)
    %assign attributes   = MinMaxAttributes(outp, fmiType)
    %selectfile incfile
    case %<vr>:
      *dtypeID = %<dtypeID>;
      *size = %<width>;
      return &(rtmGetY(S)->%<Identifier>);
    %selectfile xmlfile
    %if width == 1
    <%<fmiType> name="%<Identifier>" valueReference="%<vr>" causality="output"%<attributes>/>
    %else
    <%<fmiType> name="%<Identifier>" valueReference="%<vr>" causality="output"%<attributes>>
      <Dimensions>
        <Dimension start="%<width>"/>
      </Dimensions>
    </%<fmiType>>
    %endif
    %assign outputIndices = outputIndices + vr
  %endwith
  %return vr + 1
%endfunction